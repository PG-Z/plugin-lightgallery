plugins {
    id "com.github.node-gradle.node" version "5.0.0"
    id "run.halo.plugin.devtools" version "0.0.7"
    id "io.freefair.lombok" version "8.0.1"
    id 'java'
}

group 'run.halo.lightgallery'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    maven { url 'https://s01.oss.sonatype.org/content/repositories/releases' }
    maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots/' }
    maven { url 'https://repo.spring.io/milestone' }
    mavenCentral()
}

dependencies {
    implementation platform('run.halo.tools.platform:plugin:2.10.0-SNAPSHOT')
    compileOnly 'run.halo.app:api'

    testImplementation 'run.halo.app:api'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

node {
    nodeProjectDir = file("${project.projectDir}/assets")
}

tasks.register('installDepsForUI', PnpmTask) {
    args = ['install']
}

tasks.register('buildFrontend', PnpmTask) {
    args = ['build']
    dependsOn('installDepsForUI')
}

tasks.named('build').configure {
    dependsOn('buildFrontend')
}

halo {
    version = '2.13'
}
